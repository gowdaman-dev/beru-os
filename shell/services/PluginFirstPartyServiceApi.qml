import QtQuick

// Narrow proxy for the non-authentication first-party services used by the
// built-in bar. It intentionally has no generic property or method forwarding.
QtObject {
  required property string ownerPluginId
  required property string serviceId

  property bool stayAwake: false
  property bool enabled: false
  property bool doNotDisturb: false
  property var activePlayer: null
  property var sourcePlayers: []
  property bool active: false
  property var peers: []

  property var _setIdleEnabled: null
  property var _setNightlight: null
  property var _setDoNotDisturb: null
  property var _runAction: null
  property var _playerKey: null
  property var _selectPlayer: null
  property var _refresh: null

  function setIdleEnabled(value) {
    if (serviceId === "beru.idle" && _setIdleEnabled) _setIdleEnabled(!!value)
  }

  function setNightlight(value) {
    if (serviceId === "beru.nightlight" && _setNightlight) _setNightlight(!!value)
  }

  function setDoNotDisturb(value) {
    if (serviceId === "beru.notifications" && _setDoNotDisturb) _setDoNotDisturb(!!value)
  }

  function runAction(action, showFeedback, playerId) {
    if (serviceId === "beru.media" && _runAction)
      _runAction(String(action || ""), !!showFeedback, String(playerId || ""))
  }

  function playerKey(player) {
    return serviceId === "beru.media" && _playerKey ? _playerKey(player) : ""
  }

  function selectPlayer(playerId) {
    if (serviceId === "beru.media" && _selectPlayer) _selectPlayer(String(playerId || ""))
  }

  function refresh() {
    if (serviceId === "beru.remote-session" && _refresh) _refresh()
  }
}
